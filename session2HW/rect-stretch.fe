// triangle2.fe

PARAMETER rt = sqrt(3)
PARAMETER delta = 0

// For resetting form_factors in refining
define vertex attribute ref_coord real[3]
define vertex attribute old_vid  integer
define edge   attribute old_eid  integer

define facet attribute poisson_ratio real
define facet attribute form_factors real[3]
quantity stretch energy method linear_elastic global

boundary 1 parameters 1
x1: 0
x2: p1
x3: 0

boundary 2 parameters 1
x1: 1 + delta
x2: p1
x3: 0

vertices
1    0 boundary 1    ref_coord {0   0    0}
2    rt/2 boundary 1 ref_coord {0   rt/2 0}
3    1/2   rt/2   0  ref_coord {1/2 rt/2 0}
4    0 boundary 2    ref_coord {1   0    0}
5    rt/2 boundary 2 ref_coord {1   rt/2 0}

edges
1    1 2 boundary 1
2    1 3
3    1 4
4    2 3
5    3 5
6    3 4
7    4 5 boundary 2

faces
1    1  4 -2
2    2  6 -3
3    5 -7 -6

read

set facet tension 0
set facet poisson_ratio 1/3

read "../session2/refinement.cmd"

gg := {g 200};

getenergies := {
    define deltas real[12];
    deltas := {0.0001, 0.0002, 0.0005, 0.001, 0.002, 0.005, 0.01, 0.02, 0.05, 0.1, 0.1, 0.5};
    local ii;
    printf "# delta energy\n" >>> "energy.dat";
    for (ii:=1; ii<=12; ii+=1) {
        delta := deltas[ii];
        gg;
        printf "%g %g\n", deltas[ii], total_energy >> "energy.dat";
    };
}
