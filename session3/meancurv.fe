// Rig to test mean curvature

PARAMETER mh = 0.25

quantity smc info_only method sq_mean_curvature global
quantity easmc info_only method eff_area_sq_mean_curvature global
quantity nsmc info_only method normal_sq_mean_curvature global
quantity ssmc info_only method star_sq_mean_curvature global
quantity seasmc info_only method star_eff_area_sq_mean_curvature global
quantity snsmc info_only method star_normal_sq_mean_curvature global
quantity spsmc info_only method star_perp_sq_mean_curvature global

vertices
1	0 0 mh fixed
2	0 1 mh fixed
3	0 2 mh fixed
4	0 3 mh fixed
5	0 4 mh fixed
6	-1 0 0 fixed
7	-1 1 0 fixed
8	-1 2 0 fixed
9	-1 3 0 fixed
10	-1 4 0 fixed
11	1 0 0 fixed
12	1 1 0 fixed
13	1 2 0 fixed
14	1 3 0 fixed
15	1 4 0 fixed

edges
1 1 6
2 1 11
3 6 7
4 6 2
5 1 2
6 11 2
7 11 12
8 2 7
9 2 12
10 7 8
11 2 8
12 2 3
13 2 13
14 12 13
15 3 8
16 3 13
17 8 9
18 3 9
19 3 4
20 3 14
21 13 14
22 4 9
23 4 14
24 9 10
25 9 5
26 4 5
27 14 5
28 14 15
29 5 10
30 5 15

faces
1 -3 4 8
2 -1 5 -4
3 2 6 -5
4 -6 7 -9
5 -8 11 -10
6 -11 12 15
7 -12 13 -16
8 9 14 -13
9 -15 18 -17
10 -18 19 22
11 -19 20 -23
12 16 21 -20
13 -24 25 29
14 -22 26 -25
15 23 27 -26
16 -27 28 -30

read

set facet tension 0

procedure printvals(integer ind) {
    printf "star_sq_mean_curvature          %f\n", vertices[ind].ssmc;
    printf "star_eff_area_sq_mean_curvature %f\n", vertices[ind].seasmc;
    printf "star_normal_sq_mean_curvature   %f\n", vertices[ind].snsmc;
    printf "star_perp_sq_mean_curvature     %f\n", vertices[ind].spsmc;
}
