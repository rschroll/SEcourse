PARAMETER rt = sqrt(3)
PARAMETER delta = 0
PARAMETER gridsize = 1.0
PARAMETER thicknezz = 1.0
PARAMETER lngth = 1

// For resetting form_factors in refining
define vertex attribute ref_coord real[3]
define vertex attribute old_vid  integer
define edge   attribute old_eid  integer

define facet attribute poisson_ratio real
define facet attribute form_factors real[3]
quantity stretch energy method linear_elastic global

quantity bend energy method star_perp_sq_mean_curvature
quantity gbend energy method star_gauss_curvature

define edge attribute border integer
define edge attribute sqcurve_string_mark integer
quantity freeedgebend energy method sqcurve_string_marked
    parameter_1: 2

quantity ycenter conserved method vertex_scalar_integral global
    scalar_integrand: y

boundary 1 parameters 1
x1: 0
x2: p1
x3: 0

boundary 2 parameters 1
x1: 1 + delta
x2: p1
x3: 0

vertices
1    0 boundary 1    ref_coord {0   0    0} freeedgebend
2    rt/2 boundary 1 ref_coord {0   rt/2 0} freeedgebend
3    1/2   rt/2   0  ref_coord {1/2 rt/2 0} freeedgebend
4    0 boundary 2    ref_coord {1   0    0} freeedgebend
5    rt/2 boundary 2 ref_coord {1   rt/2 0} freeedgebend

edges
1    1 2 boundary 1 border 1
2    1 3
3    1 4 border 1 sqcurve_string_mark 1
4    2 3 border 1 sqcurve_string_mark 1
5    3 5 border 1 sqcurve_string_mark 1
6    3 4
7    4 5 boundary 2 border 1

faces
1    1  4 -2
2    2  6 -3
3    5 -7 -6

read

set facet tension 0
set facet poisson_ratio 1/3

read "refinement.cmd"

set_thickness(0.1)

read "diagnostics.cmd"

r 2
U
hessian_normal off
delta := -0.1
g 10
delta := -0.2
g 20
